<!DOCTYPE html>
<html>
  <head>
    <title>W3P1</title>
    <script type="text/javascript" src="w5p4.js"></script>
    <script type="text/javascript" src="../common/OBJParser.js"></script>
    <script id="wgsl" type="x-shader">
        @group(0) @binding(0) var<uniform> uniforms_f : Uniforms_f;
        @group(0) @binding(1) var<uniform> uniforms_ui: Uniforms_ui;
        @group(0) @binding(2) var my_texture: texture_2d<f32>;
        @group(0) @binding(3) var<storage> jitter: array<vec2f>;
        @group(0) @binding(4) var<storage> vPositions: array<vec3f>;
        @group(0) @binding(5) var<storage> meshFaces: array<vec3u>;
        @group(0) @binding(6) var<storage> normals: array<vec3f>;
        @group(0) @binding(7) var<storage> materials: array<material>;
        @group(0) @binding(8) var<storage> materialIndices: array<u32>;

        struct Uniforms_f {
          aspect: f32,
          cam_const: f32,
          gamma: f32,
          shader: f32,
        };

        struct Uniforms_ui {
          use_repeat: u32,
          use_linear: u32,
          number_of_jitters: u32,
        };

        struct VSOut {
          @builtin(position) position: vec4f,
          @location(0) coords : vec2f,
        };

        @vertex
        fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut {
          const pos = array<vec2f, 4>(vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
          var vsOut: VSOut;
          vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
          vsOut.coords = pos[VertexIndex];
          return vsOut;
        }

        struct Ray {
          origin : vec3f,
          direction : vec3f,
          tmin : f32,
          tmax : f32,
        };

        struct HitInfo {
          has_hit: bool,
          dist: f32,
          position: vec3f,
          normal: vec3f,
          color: vec3f,
          shader: u32,
          ior1_over_ior2: f32,
          texcoords: vec2f,
        };

        struct Light {
          L_i: vec3f,
          w_i: vec3f,
          dist: f32,
        }

        struct Onb {
          tangent: vec3f, 
          binormal: vec3f,
          normal: vec3f,
        }

        struct material {
          diffuse: vec4f,
          emission: vec4f,
        }

        fn intersect_plane(r: Ray, hit: ptr<function, HitInfo>, position: vec3f, onb: Onb) -> bool {
          let t_prime = dot((position - r.origin), onb.normal) / dot(r.direction, onb.normal);

          if (t_prime > r.tmin && t_prime < r.tmax) {
            let hit_pos = r.origin + t_prime * r.direction;

            let b1 = onb.tangent;
            let b2 = onb.binormal;

            let texScale = 0.2;
    
            let u = dot((hit_pos - position), b1) * texScale;
            let v = dot((hit_pos - position), b2) * texScale;

            let color = select(texture_nearest(my_texture, vec2f(u, v), uniforms_ui.use_repeat != 0), texture_linear(my_texture, vec2f(u, v), uniforms_ui.use_repeat != 0), uniforms_ui.use_linear != 0);

            (*hit).has_hit = true;
            (*hit).dist = t_prime;
            (*hit).position = hit_pos;
            (*hit).normal = onb.normal;
            (*hit).color = color;
            (*hit).shader = 0;
            (*hit).texcoords = vec2f(u, v);
            return true;
          }

          return false;
        }

        fn intersect_triangle(r: Ray, hit: ptr<function, HitInfo>, faceIndex: u32) -> bool {
          let face = meshFaces[faceIndex];

          let v0 = vPositions[face[0]];
          let v1 = vPositions[face[1]];
          let v2 = vPositions[face[2]];

          let e_0 = v1 - v0;
          let e_1 = v2 - v0;
          let normal = cross(e_0, e_1);

          let cross_result = cross((v0 - r.origin), r.direction);
          let denominator = dot(r.direction, normal);

          let t_prime = dot((v0 - r.origin), normal) / denominator;
          let beta = dot(cross_result, e_1) / denominator;
          let gamma = -dot(cross_result, e_0) / denominator;
          let alpha = 1.0 - beta - gamma;

          let n0 = normals[face[0]];
          let n1 = normals[face[1]];
          let n2 = normals[face[2]];

          let mat_index = materialIndices[faceIndex];
          let mat = materials[mat_index];

          if (r.tmin < t_prime && r.tmax > t_prime) {
            if (beta >= 0.0 && gamma >= 0.0 && beta + gamma <= 1.0) {
              (*hit).has_hit = true;
              (*hit).dist = t_prime;
              (*hit).position = r.origin + t_prime * r.direction;
              (*hit).normal = normalize(alpha*n0 + beta*n1 + gamma*n2);
              (*hit).color = mat.diffuse.rgb;
              (*hit).shader = 0;
              return true;
            }
          }

          return false;
        }

        fn sample_point_light(pos: vec3f) -> Light {
          let p = vec3f(0.0, 1.0, 0.0);
          let I = vec3f(3.14159);

          let L_i = I / (length(p - pos) * length(p - pos));

          let light = Light (
            L_i,
            normalize(p - pos),
            length(p - pos)
          );

          return light;
        }

        fn sample_directional_light(pos: vec3f) -> Light {
          let L_e = vec3f(3.14159);

          let light = Light (
            L_e,
            -normalize(vec3f(-1.0)),
            1000,
          );

          return light;
        }

        fn lambertian(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let light = sample_directional_light(hit.position);

          var shadow_ray = Ray(
            hit.position,
            light.w_i,
            1e-4,
            light.dist - 1e-4
          );

          var shadow_hit = HitInfo(
            false,
            hit.dist,
            hit.position,
            hit.normal,
            vec3f(0.0),
            1,
            1.5,
            vec2f(0.0, 0.0),
          );

          let L_a = hit.color * 0.1;
          let L_e = 0.0;

          let L_r = ((hit.color * 0.9) / 3.14159) * light.L_i * dot(hit.normal,light.w_i) + L_a;

          if (intersect_scene(&shadow_ray, &shadow_hit)) {
            return L_a;
          }

          return L_e + L_r + L_a;
        }

        fn mirror(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let A = r.origin;
          let B = hit.position;
          let w_i = (A - B) / length(A - B);

          let cos_theta = dot(w_i,hit.normal);

          // Re-emit the ray with new direction and origin. Don't return anything for this hit.
          r.direction = 2 * cos_theta * hit.normal - w_i;
          r.origin = hit.position;
          r.tmin = 1e-4;
          r.tmax = 100000;
          hit.has_hit = false;

          return vec3f(0.0);
        }

        fn refractive(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let A = r.origin;
          let B = hit.position;
          let w_i = (A - B) / length(A - B);

          let cos_theta = dot(w_i,hit.normal);

          let cos_theta_t_sqaured = 1 - hit.ior1_over_ior2 * hit.ior1_over_ior2 * (1-dot(w_i, hit.normal)*dot(w_i, hit.normal));

          var direction = hit.ior1_over_ior2 * (dot(w_i, hit.normal) * hit.normal - w_i) - hit.normal * sqrt(cos_theta_t_sqaured);
          if (cos_theta_t_sqaured < 0) {
            direction = 2 * cos_theta * hit.normal - w_i;
          }

          // Re-emit the ray with new direction and origin. Don't return anything for this hit.
          r.direction = direction;
          r.origin = hit.position;
          r.tmin = 1e-4;
          r.tmax = 100000;
          hit.has_hit = false;

          return vec3f(0.0);
        }

        fn phong(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let light = sample_directional_light(hit.position);
          var shadow_ray = Ray(
            hit.position,
            light.w_i,
            1e-4,
            light.dist - 1e-4
          );

          var shadow_hit = HitInfo(
            false,
            hit.dist,
            hit.position,
            hit.normal,
            vec3f(0.0),
            1,
            1.5,
            vec2f(0.0, 0.0),
          );

          let L_a = hit.color * 0.1;

          let A = r.origin;
          let B = hit.position;

          let rho_s = 0.1;
          let rho_d = hit.color * 0.9;
          let s = 42.0;
          let pi = 3.14159;

          let w_0 = reflect(r.direction, hit.normal) / length(r.direction);
          let w_i = light.w_i;

          if (intersect_scene(&shadow_ray, &shadow_hit)) {
            return L_a;
          }

          let L_r =((rho_d / pi) + (rho_s * (s + 2) / (2 * pi)) * pow(max(dot(w_0, w_i), 0.0), s)) * light.L_i * max(dot(w_i, hit.normal), 0.0);

          return L_r;
        }

        fn glossy(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let L_r = phong(r, hit);
          return L_r + refractive(r, hit);
        }

        // Textures
        fn texture_nearest(texture: texture_2d<f32>, texcoords: vec2f, repeat: bool) -> vec3f {
          let res = textureDimensions(texture);
          let st = select(clamp(texcoords, vec2f(0), vec2f(1)), texcoords - floor(texcoords), repeat);
          let ab = st*vec2f(res);
          let UV = vec2u(ab + 0.5) % res;
          let texcolor = textureLoad(texture, UV, 0);
          return texcolor.rgb;
        }

        fn texture_linear(texture: texture_2d<f32>, texcoords: vec2f, repeat: bool) -> vec3f { 
          let res = textureDimensions(texture);
          let st = select(clamp(texcoords, vec2f(0), vec2f(1)), texcoords - floor(texcoords), repeat);
          let ab = st*vec2f(res);

          let UV = vec2u(ab);
          let UVP1 = (UV + 1) % res;
          let UVP1x = vec2u(UVP1.x, UV.y);
          let UVP1y = vec2u(UV.x, UVP1.y);

          let c = ab % 1;

          let color1 = textureLoad(texture, UV, 0);
          let color2 = textureLoad(texture, UVP1x, 0);
          let color3 = textureLoad(texture, UVP1y, 0); 
          let color4 = textureLoad(texture, UVP1, 0);          

          let interp1 = mix(color1, color2, c.x);
          let interp2 = mix(color3, color4, c.x);
          let interp3 = mix(interp1, interp2, c.y);

          return interp3.rgb;
        } 

        fn get_camera_ray(ipcoords: vec2f) -> Ray
        {
          const eye = vec3f(277.0, 275.0, -570.0);
          const lookAt = vec3f(277.0, 275.0, 0.0);
          const u = vec3f(0.0, 1.0, 0.0);

          let v = normalize(lookAt - eye);

          let b1 = normalize(cross(v, u));
          let b2 = cross(b1, v);

          var ray: Ray;
          ray.origin = eye;
          ray.direction = normalize(ipcoords.x*b1 + ipcoords.y*b2 + uniforms_f.cam_const * v);
          ray.tmin = 0.0;
          ray.tmax = 1.0e16;

          return ray;
        }

        fn shade(r: ptr<function, Ray>,hit: ptr<function, HitInfo>) -> vec3f {
          switch (*hit).shader {
          case 1 { return lambertian(r, hit); }
          case 2 { return phong(r, hit); }
          case 3 { return mirror(r, hit); }
          case 4 { return refractive(r, hit); }
          case 5 { return glossy(r, hit); }
          case default { return (*hit).color; }
        }
      }

        fn intersect_scene(r: ptr<function, Ray>, hit : ptr<function, HitInfo>) -> bool {
          // plane
          let position = vec3f(0.0, 0.0, 0.0);
          const plane_onb = Onb(vec3f(-1.0, 0.0, 0.0), vec3f(0.0, 0.0, 1.0), vec3f(0.0, 1.0, 0.0));
          let p_color = vec3f(0.1, 0.7, 0.0);

          // Check if the ray intersects the plane
          if intersect_plane((*r), hit, position, plane_onb) {
            hit.shader = 1;
            r.tmax = hit.dist;
          }
          
          // Check ray intersect with triangels
          for (var i = 0u; i < arrayLength(&meshFaces); i = i + 1u) {
            if intersect_triangle((*r), hit, i) {
              hit.shader = 1;
              r.tmax = hit.dist;
            }
          }

          return hit.has_hit;
        }

        @fragment
        fn main_fs(@location(0) coords: vec2f) -> @location(0) vec4f {
          const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
          const max_depth = 10;
          let uv = vec2f(coords.x*uniforms_f.aspect*0.5f, coords.y*0.5f);
          
          var result = vec3f(0.0);

          let use_repeat = uniforms_ui.use_repeat != 0; let use_linear = uniforms_ui.use_linear != 0;
          let color = select(texture_nearest(my_texture, uv, use_repeat), texture_linear(my_texture, uv, use_repeat), use_linear);

          for(var j = 0u; j < uniforms_ui.number_of_jitters; j++) {
            var r = get_camera_ray(uv + jitter[j]);
            var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 1.5, vec2f(0.0, 0.0));
            for(var i = 0; i < max_depth; i++) {
              if (intersect_scene(&r, &hit)) {
                  result += shade(&r, &hit);
              } else {
                  result += bgcolor.rgb; break;
              }

              if hit.has_hit {
                  break;
              }
            }
          } 

          result /= f32(uniforms_ui.number_of_jitters);
          return vec4f(pow(result, vec3f(1.0/uniforms_f.gamma)), bgcolor.a);
      }
    </script>
  </head>
  <body>
    <canvas id="webgpu-canvas" width="512" height="512">
      Please use a browser that supports HTML5 canvas.
    </canvas>
    <select id="shaderMenu">
      <option value="0">Lambertian</option>
      <option value="1">Phong</option>
      <option value="2">Mirror</option>
      <option value="3">Refractive</option>
      <option value="4" selected>Glossy</option>
    </select>
    <select id="addressmode" size="2">
      <option value="0">clamp-to-edge</option>
      <option value="1" selected>repeat</option>
    </select>
    <select id="filtermode" size="2">
      <option value="0">nearest</option>
      <option value="1" selected>linear</option>
    </select>
    <button id="inc_subdiv">Increase subdivisions</button>
    <button id="dec_subdiv">Decrease subdivisions</button>
  </body>
</html>
