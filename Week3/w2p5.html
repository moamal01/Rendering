<!DOCTYPE html>
<html>
  <head>
    <title>W2P5</title>
    <script type="text/javascript" src="w2p5.js"></script>
    <script id="wgsl" type="x-shader">
        struct Uniforms {
          aspect: f32,
          cam_const: f32,
          gamma: f32,
          shader: f32,
        };
        @group(0) @binding(0) var<uniform> uniforms : Uniforms;

        struct VSOut {
          @builtin(position) position: vec4f,
          @location(0) coords : vec2f,
        };

        @vertex
        fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut
        {
          const pos = array<vec2f, 4>(vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
          var vsOut: VSOut;
          vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
          vsOut.coords = pos[VertexIndex];
          return vsOut;
        }

        struct Ray {
          origin : vec3f,
          direction : vec3f,
          tmin : f32,
          tmax : f32,
        };

        struct HitInfo {
          has_hit: bool,
          dist: f32,
          position: vec3f,
          normal: vec3f,
          color: vec3f,
          shader: u32,
          ior1_over_ior2: f32,
        };

        struct Light {
          L_i: vec3f,
          w_i: vec3f,
          dist: f32,
        }

        fn intersect_plane(r: Ray, hit: ptr<function, HitInfo>, position: vec3f, normal: vec3f) -> bool {
          let t_prime = dot((position - r.origin), normal) / dot(r.direction, normal);

          if (t_prime > r.tmin && t_prime < r.tmax) {
            (*hit).has_hit = true;
            (*hit).dist = t_prime;
            (*hit).position = r.origin + t_prime * r.direction;
            (*hit).normal = normal;
            (*hit).color = vec3f(0.1, 0.7, 0.0);
            (*hit).shader = 0;
            return true;
          }

          return false;
        }

        fn intersect_triangle(r: Ray, hit: ptr<function, HitInfo>, v: array<vec3f, 3>) -> bool {
          let e_0 = v[1] - v[0];
          let e_1 = v[2] - v[0];
          let normal = cross(e_0, e_1);

          let cross_result = cross((v[0] - r.origin), r.direction);
          let denominator = dot(r.direction, normal);

          let t_prime = dot((v[0] - r.origin), normal) / denominator;
          let beta = dot(cross_result, e_1) / denominator;
          let gamma = -dot(cross_result, e_0) / denominator;
          let alpha = 1.0 - beta - gamma;

          if (r.tmin < t_prime && r.tmax > t_prime) {
            if (beta >= 0.0 && gamma >= 0.0 && beta + gamma <= 1.0) {
              (*hit).has_hit = true;
              (*hit).dist = t_prime;
              (*hit).position = r.origin + t_prime * r.direction;
              (*hit).normal = normal / length(normal);
              (*hit).color = vec3f(0.4, 0.3, 0.2);
              (*hit).shader = 0;
              return true;
            }
          }

          return false;
        }

        fn intersect_sphere(r: Ray, hit: ptr<function, HitInfo>, center: vec3f, radius: f32) -> bool {
          let origin_minus_center = r.origin - center;

          let a = dot(r.direction, r.direction);
          let b_half = dot(origin_minus_center, r.direction);
          let c = dot(origin_minus_center, origin_minus_center) - radius * radius;

          if b_half * b_half - c < 0 {
            return false;
          }

          let t1_prime = -b_half - sqrt(b_half*b_half - c);
          var p = r.origin + t1_prime * r.direction;
          var normal = p - center;

          if t1_prime > r.tmin && t1_prime < r.tmax {
            (*hit).has_hit = true;
            (*hit).dist = t1_prime;
            (*hit).position = p;
            (*hit).normal = normal / length(normal);
            (*hit).color = vec3f(0.0, 0.0, 0.0);
            (*hit).shader = 0;
            (*hit).ior1_over_ior2 = 1.5;
            return true;
          }

          let t2_prime = -b_half + sqrt(b_half*b_half - c);

          if t2_prime > r.tmin && t2_prime < r.tmax {
            var p = r.origin + t2_prime * r.direction;
            var normal = p - center;

            (*hit).has_hit = true;
            (*hit).dist = t2_prime;
            (*hit).position = p;
            (*hit).normal = normal / length(normal);
            (*hit).color = vec3f(0.0, 0.0, 0.0);
            (*hit).shader = 0;
            (*hit).ior1_over_ior2 = 1.5;
            return true;
          }

          return false;
        }

        fn sample_point_light(pos: vec3f) -> Light {
          let p = vec3f(0.0, 1.0, 0.0);
          let I = vec3f(3.14159);

          let L_i = I / (length(p - pos) * length(p - pos));

          let light = Light (
            L_i,
            normalize(p - pos),
            length(p - pos)
          );

          return light;
        }

        fn lambertian(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let light = sample_point_light(hit.position);

          var shadow_ray = Ray(
            hit.position,
            light.w_i,
            1e-4,
            light.dist - 1e-4
          );

          var shadow_hit = HitInfo(
            false,
            hit.dist,
            hit.position,
            hit.normal,
            vec3f(0.0),
            1,
            1.5,
          );

          let L_a = hit.color * 0.1;
          let L_e = 0.0;

          let L_r = ((hit.color * 0.9) / 3.14159) * light.L_i * dot(hit.normal,light.w_i) + L_a;

          if (intersect_scene(&shadow_ray, &shadow_hit)) {
            return L_a;
          }

          return L_e + L_r + L_a;
        }

        fn mirror(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let light = sample_point_light(hit.position);

          let A = r.origin;
          let B = hit.position;
          let w_i = (A - B) / length(A - B);

          let cos_theta = dot(w_i,hit.normal);

          // Re-emit the ray with new direction and origin. Don't return anything for this hit.
          r.direction = 2 * cos_theta * hit.normal - w_i;
          r.origin = hit.position;
          r.tmin = 1e-4;
          r.tmax = 100000;
          hit.has_hit = false;

          return vec3f(0.0);
        }

        fn refractive(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let light = sample_point_light(hit.position);

          let A = r.origin;
          let B = hit.position;
          let w_i = (A - B) / length(A - B);

          let cos_theta = dot(w_i,hit.normal);

          let cos_theta_t_sqaured = 1 - hit.ior1_over_ior2 * hit.ior1_over_ior2 * (1-dot(w_i, hit.normal)*dot(w_i, hit.normal));

          var direction = hit.ior1_over_ior2 * (dot(w_i, hit.normal) * hit.normal - w_i) - hit.normal * sqrt(cos_theta_t_sqaured);
          if (cos_theta_t_sqaured < 0) {
            direction = 2 * cos_theta * hit.normal - w_i;
          }

          // Re-emit the ray with new direction and origin. Don't return anything for this hit.
          r.direction = direction;
          r.origin = hit.position;
          r.tmin = 1e-4;
          r.tmax = 100000;
          hit.has_hit = false;

          return vec3f(0.0);
        }

        fn phong(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let light = sample_point_light(hit.position);
          var shadow_ray = Ray(
            hit.position,
            light.w_i,
            1e-4,
            light.dist - 1e-4
          );

          var shadow_hit = HitInfo(
            false,
            hit.dist,
            hit.position,
            hit.normal,
            vec3f(0.0),
            1,
            1.5,
          );

          let L_a = hit.color * 0.1;

          let A = r.origin;
          let B = hit.position;

          let rho_s = 0.1;
          let rho_d = hit.color * 0.9;
          let s = 42.0;
          let pi = 3.14159;

          let w_0 = reflect(r.direction, hit.normal) / length(r.direction);
          let w_i = light.w_i;

          if (intersect_scene(&shadow_ray, &shadow_hit)) {
            return L_a;
          }

          let L_r =((rho_d / pi) + (rho_s * (s + 2) / (2 * pi)) * pow(max(dot(w_0, w_i), 0.0), s)) * light.L_i * max(dot(w_i, hit.normal), 0.0);

          return L_r;
        }

        fn glossy(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let L_r = phong(r, hit);
          return L_r + refractive(r, hit);
        }

        fn get_camera_ray(ipcoords: vec2f) -> Ray
        {
          const eye = vec3f(2.0, 1.5, 2.0);
          const p = vec3f(0.0, 0.5, 0.0);
          const u = vec3f(0.0, 1.0, 0.0);

          let v = normalize(p - eye);

          let b1 = normalize(cross(v, u));
          let b2 = cross(b1, v);
          let d = 1.0;

          var ray: Ray;
          ray.origin = eye;
          ray.direction = normalize(ipcoords.x*b1 + ipcoords.y*b2 + uniforms.cam_const * v);
          ray.tmin = 0.0;
          ray.tmax = 1.0e16;

          return ray;
        }

        fn shade(r: ptr<function, Ray>,hit: ptr<function, HitInfo>) -> vec3f {
          switch (*hit).shader {
          case 1 { return lambertian(r, hit); }
          case 2 { return phong(r, hit); }
          case 3 { return mirror(r, hit); }
          case 4 { return refractive(r, hit); }
          case 5 { return glossy(r, hit); }
          case default { return (*hit).color; }
        }
      }

        fn intersect_scene(r: ptr<function, Ray>, hit : ptr<function, HitInfo>) -> bool {
          // plane
          let position = vec3f(0.0, 0.0, 0.0);
          let normal = vec3f(0.0, 1.0, 0.0);
          let p_color = vec3f(0.1, 0.7, 0.0);

          // triangle
          let v0 = vec3f(-0.2, 0.1, 0.9);
          let v1 = vec3f(0.2, 0.1, 0.9);
          let v2 = vec3f(-0.2, 0.1, -0.1);

          // sphere
          let center = vec3f(0.0, 0.5, 0.0);
          let radius = 0.3;
          let refractive_index = 1.5;
          let shininess = 42;

          // Check if the ray intersects the plane
          if intersect_plane((*r), hit, position, normal) {
            hit.shader = 1;
            r.tmax = hit.dist;
          }

          if intersect_triangle((*r), hit, array<vec3f, 3>(v0, v1, v2)) {
            hit.shader = 1;
            r.tmax = hit.dist;
          }

          if intersect_sphere((*r), hit, center, radius) {
            hit.shader = u32(uniforms.shader);

            if (dot(r.direction, hit.normal) > 0) {
              hit.ior1_over_ior2 = refractive_index;
              hit.normal = -hit.normal;
            } else {
              hit.ior1_over_ior2 = 1 / refractive_index;
            }

            r.tmax = hit.dist;
          }

          return hit.has_hit;
        }

        @fragment
        fn main_fs(@location(0) coords: vec2f) -> @location(0) vec4f {
            const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
            const max_depth = 10;
            let uv = vec2f(coords.x*uniforms.aspect*0.5f, coords.y*0.5f);
            var r = get_camera_ray(uv);
            var result = vec3f(0.0);
            var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 1.5);
            for(var i = 0; i < max_depth; i++) {
                if(intersect_scene(&r, &hit)) {
                    result += shade(&r, &hit);
                } else {
                    result += bgcolor.rgb; break;
                }

                if hit.has_hit {
                    break;
                }
            }

            return vec4f(pow(result, vec3f(1.0/uniforms.gamma)), bgcolor.a);
        }
    </script>
  </head>
  <body>
    <canvas id="webgpu-canvas" width="512" height="512">
      Please use a browser that supports HTML5 canvas.
    </canvas>
    <select id="shaderMenu">
      <option value="0">Lambertian</option>
      <option value="1">Phong</option>
      <option value="2">Mirror</option>
      <option value="3">Refractive</option>
      <option value="4" selected>Glossy</option>
    </select>
  </body>
</html>
