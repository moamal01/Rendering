<!DOCTYPE html>
<html>
  <head>
    <title>W3P1</title>
    <script type="text/javascript" src="w6p2.js"></script>
    <script type="text/javascript" src="../bsptree/OBJParser_interleaved.js"></script>
    <script type="text/javascript" src="../bsptree/BspTree_interleaved.js"></script>
    <script type="text/javascript" src="../bsptree/Aabb.js"></script>
    <script src="../angelCommon/MV.js" type="text/javascript"></script>
    <script id="wgsl" type="x-shader">
        @group(0) @binding(0) var<uniform> uniforms_f : Uniforms_f;
        @group(0) @binding(1) var<uniform> uniforms_ui: Uniforms_ui;
        @group(0) @binding(2) var my_texture: texture_2d<f32>;
        @group(0) @binding(3) var<storage> jitter: array<vec2f>;
        @group(0) @binding(4) var<storage> attributes: array<Vertex_attributes>;
        @group(0) @binding(5) var<storage> meshFaces: array<vec4u>;
        @group(0) @binding(6) var<storage> materials: array<material>;
        //@group(0) @binding(7) var<storage> materialIndices: array<u32>;
        //@group(0) @binding(9) var<uniform> lightIndices: LightIndices;

        @group(0) @binding(7) var<uniform> aabb: Aabb;
        @group(0) @binding(8) var<storage> treeIds: array<u32>;
        @group(0) @binding(9) var<storage> bspTree: array<vec4u>;
        @group(0) @binding(10) var<storage> bspPlanes: array<f32>;

        const MAX_LEVEL = 20u;
        const BSP_LEAF = 3u;
        var<private> branch_node: array<vec2u, MAX_LEVEL>;
        var<private> branch_ray: array<vec2f, MAX_LEVEL>;

        struct Uniforms_f {
          aspect: f32,
          cam_const: f32,
          gamma: f32,
          shader: f32,
        };

        struct Uniforms_ui {
          use_repeat: u32,
          use_linear: u32,
          number_of_jitters: u32,
        };

        struct VSOut {
          @builtin(position) position: vec4f,
          @location(0) coords : vec2f,
        };

        struct Vertex_attributes {
          vPosition: vec3f,
          normal: vec3f,
        }

        @vertex
        fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut {
          const pos = array<vec2f, 4>(vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
          var vsOut: VSOut;
          vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
          vsOut.coords = pos[VertexIndex];
          return vsOut;
        }

        struct Ray {
          origin : vec3f,
          direction : vec3f,
          tmin : f32,
          tmax : f32,
        };

        struct HitInfo {
          has_hit: bool,
          dist: f32,
          position: vec3f,
          normal: vec3f,
          color: vec3f,
          shader: u32,
          ior1_over_ior2: f32,
          texcoords: vec2f,
          emission: vec3f,
        };

        struct Light {
          L_i: vec3f,
          w_i: vec3f,
          dist: f32,
        }

        struct Onb {
          tangent: vec3f, 
          binormal: vec3f,
          normal: vec3f,
        }

        struct material {
          diffuse: vec4f,
          emission: vec4f,
        }

        struct Aabb {
          min: vec3f,
          max: vec3f,
        }

        fn intersect_sphere(r: Ray, hit: ptr<function, HitInfo>, center: vec3f, radius: f32) -> bool {
          let origin_minus_center = r.origin - center;

          let a = dot(r.direction, r.direction);
          let b_half = dot(origin_minus_center, r.direction);
          let c = dot(origin_minus_center, origin_minus_center) - radius * radius;

          if b_half * b_half - c < 0 {
            return false;
          }

          let t1_prime = -b_half - sqrt(b_half*b_half - c);
          var p = r.origin + t1_prime * r.direction;
          var normal = p - center;

          if t1_prime > r.tmin && t1_prime < r.tmax {
            (*hit).has_hit = true;
            (*hit).dist = t1_prime;
            (*hit).position = p;
            (*hit).normal = normal / length(normal);
            (*hit).color = vec3f(0.0, 0.0, 0.0);
            (*hit).shader = 1;
            (*hit).ior1_over_ior2 = 1.5;
            return true;
          }

          let t2_prime = -b_half + sqrt(b_half*b_half - c);

          if t2_prime > r.tmin && t2_prime < r.tmax {
            var p = r.origin + t2_prime * r.direction;
            var normal = p - center;

            (*hit).has_hit = true;
            (*hit).dist = t2_prime;
            (*hit).position = p;
            (*hit).normal = normal / length(normal);
            (*hit).color = vec3f(0.0, 0.0, 0.0);
            (*hit).shader = 1;
            (*hit).ior1_over_ior2 = 1.5;
            return true;
          }

          return false;
        }

        fn intersect_triangle(r: Ray, hit: ptr<function, HitInfo>, faceIndex: u32) -> bool {
          let face = meshFaces[faceIndex];

          let v0 = attributes[face[0]].vPosition;
          let v1 = attributes[face[1]].vPosition;
          let v2 = attributes[face[2]].vPosition;

          let e_0 = v1 - v0;
          let e_1 = v2 - v0;
          let normal = cross(e_0, e_1);

          let cross_result = cross((v0 - r.origin), r.direction);
          let denominator = dot(r.direction, normal);

          if (abs(denominator) < 1e-8) {
            return false;
          }

          let t_prime = dot((v0 - r.origin), normal) / denominator;
          let beta = dot(cross_result, e_1) / denominator;
          let gamma = -dot(cross_result, e_0) / denominator;
          let alpha = 1.0 - beta - gamma;

          let n0 = attributes[face[0]].normal;
          let n1 = attributes[face[1]].normal;
          let n2 = attributes[face[2]].normal;

          let mat_index = face[3];
          let mat = materials[mat_index];

          if (r.tmin < t_prime && r.tmax > t_prime) {
            if (beta >= 0.0 && gamma >= 0.0 && beta + gamma <= 1.0) {
              (*hit).has_hit = true;
              (*hit).dist = t_prime;
              (*hit).position = r.origin + t_prime * r.direction;
              (*hit).normal = normalize(alpha*n0 + beta*n1 + gamma*n2);
              (*hit).color = mat.diffuse.rgb;
              (*hit).shader = 1;
              (*hit).emission = mat.emission.rgb;
              return true;
            }
          }

          return false;
        }

        fn intersect_min_max(r: ptr<function, Ray>) -> bool {
          let p1 = (aabb.min - r.origin)/r.direction;
          let p2 = (aabb.max - r.origin)/r.direction;
          let pmin = min(p1, p2);
          let pmax = max(p1, p2);
          let tmin = max(pmin.x, max(pmin.y, pmin.z));
          let tmax = min(pmax.x, min(pmax.y, pmax.z));
          if (tmin > tmax || tmin > r.tmax || tmax < r.tmin) {
            return false;
          }
          r.tmin = max(tmin - 1.0e-3f, r.tmin);
          r.tmax = min(tmax + 1.0e-3f, r.tmax);

          return true;
        }

        fn intersect_trimesh(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> bool {
          var branch_lvl = 0u;
          var near_node = 0u;
          var far_node = 0u;
          var t = 0.0f;
          var node = 0u;
          for(var i = 0u; i <= MAX_LEVEL; i++) {
            let tree_node = bspTree[node];
            let node_axis_leaf = tree_node.x&3u;
            if(node_axis_leaf == BSP_LEAF) {
              // A leaf was found
              let node_count = tree_node.x>>2u;
              let node_id = tree_node.y;
              var found = false;
              for(var j = 0u; j < node_count; j++) {
                let obj_idx = treeIds[node_id + j];
                if(intersect_triangle(*r, hit, obj_idx)) {
                  r.tmax = hit.dist;
                  found = true;
                }
              }
              if(found) { return true; }
              else if(branch_lvl == 0u) { return false; }
              else {
                branch_lvl--;
                i = branch_node[branch_lvl].x;
                node = branch_node[branch_lvl].y;
                r.tmin = branch_ray[branch_lvl].x;
                r.tmax = branch_ray[branch_lvl].y;
                continue;
              }
            }
            let axis_direction = r.direction[node_axis_leaf];
            let axis_origin = r.origin[node_axis_leaf];
            if(axis_direction >= 0.0f) {
              near_node = tree_node.z; // left
              far_node = tree_node.w; // right
            }
            else {
              near_node = tree_node.w; // right
              far_node = tree_node.z; // left
            }
            let node_plane = bspPlanes[node];
            let denom = select(axis_direction, 1.0e-8f, abs(axis_direction) < 1.0e-8f);
            t = (node_plane - axis_origin)/denom;
            if(t > r.tmax) { node = near_node; }
            else if(t < r.tmin) { node = far_node; }
            else {
              branch_node[branch_lvl].x = i;
              branch_node[branch_lvl].y = far_node;
              branch_ray[branch_lvl].x = t;
              branch_ray[branch_lvl].y = r.tmax;
              branch_lvl++;
              r.tmax = t;
              node = near_node;
            }
          }

        return false;
      }

      fn sample_point_light(pos: vec3f) -> Light {
            let face1 = meshFaces[2];
            let face2 = meshFaces[3];

            let v0 = attributes[face1[0]].vPosition;
            let v1 = attributes[face1[1]].vPosition;
            let v2 = attributes[face1[2]].vPosition;
            let v3 = attributes[face2[0]].vPosition;
            let v4 = attributes[face2[1]].vPosition;
            let v5 = attributes[face2[2]].vPosition;

            let edge1 = v0 - v1;
            let edge2 = v0 - v2;

            let a_triangle1 = length(cross(edge1, edge2)) / 2.0;
            let n_triangle1 = cross(edge1, edge2);

            let edge3 = v3 - v4;
            let edge4 = v3 - v5;

            let a_triangle2 = length(cross(edge3, edge4)) / 2.0;
            let n_triangle2 = cross(edge3, edge4);

            let material1 = materials[face1[3]];
            let material2 = materials[face2[3]];

            let L_e = (material1.emission + material2.emission).rgb / 2.0;
            let A = a_triangle1 + a_triangle2;
            let avg_pos = (v0 + v1 + v2 + v3 + v4 + v5) / 6.0;
            let n = normalize(n_triangle1 + n_triangle2);
            let L_i = dot(n, -normalize(avg_pos - pos)) * (L_e * A) / ((length(avg_pos - pos)) * length((avg_pos - pos)));

            let light = Light (
                L_i,
                normalize(avg_pos - pos),
                length(avg_pos - pos)
            );
            return light;
        } 

        fn sample_directional_light(pos: vec3f) -> Light {
          let L_e = vec3f(3.14159);

          let light = Light (
            L_e,
            -normalize(vec3f(-1.0)),
            1000,
          );

          return light;
        }

        fn lambertian(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let light = sample_point_light(hit.position);

          var shadow_ray = Ray(
            hit.position,
            light.w_i,
            1e-3,
            light.dist - 1e-3
          );

          var shadow_hit = HitInfo(
            false,
            hit.dist,
            hit.position,
            hit.normal,
            vec3f(0.0),
            1,
            1.5,
            vec2f(0.0),
            vec3f(0.0),
          );

          let L_e = hit.emission;

          let L_r = ((hit.color * 0.9) / 3.14159) * light.L_i * dot(hit.normal,light.w_i);

          if (intersect_scene(&shadow_ray, &shadow_hit)) {
            return L_e;
          }

          return L_e + L_r;
        }

        fn mirror(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let A = r.origin;
          let B = hit.position;
          let w_i = (A - B) / length(A - B);

          let cos_theta = dot(w_i,hit.normal);

          // Re-emit the ray with new direction and origin. Don't return anything for this hit.
          r.direction = 2 * cos_theta * hit.normal - w_i;
          r.origin = hit.position;
          r.tmin = 1e-4;
          r.tmax = 100000;
          hit.has_hit = false;

          return vec3f(0.0);
        }

        fn refractive(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let A = r.origin;
          let B = hit.position;
          let w_i = (A - B) / length(A - B);

          let cos_theta = dot(w_i,hit.normal);

          let cos_theta_t_sqaured = 1 - hit.ior1_over_ior2 * hit.ior1_over_ior2 * (1-dot(w_i, hit.normal)*dot(w_i, hit.normal));

          var direction = hit.ior1_over_ior2 * (dot(w_i, hit.normal) * hit.normal - w_i) - hit.normal * sqrt(cos_theta_t_sqaured);
          if (cos_theta_t_sqaured < 0) {
            direction = 2 * cos_theta * hit.normal - w_i;
          }

          // Re-emit the ray with new direction and origin. Don't return anything for this hit.
          r.direction = direction;
          r.origin = hit.position;
          r.tmin = 1e-4;
          r.tmax = 100000;
          hit.has_hit = false;

          return vec3f(0.0);
        }

        fn phong(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let light = sample_directional_light(hit.position);
          var shadow_ray = Ray(
            hit.position,
            light.w_i,
            1e-4,
            light.dist - 1e-4
          );

          var shadow_hit = HitInfo(
            false,
            hit.dist,
            hit.position,
            hit.normal,
            vec3f(0.0),
            1,
            1.5,
            vec2f(0.0),
            vec3f(0.0),
          );

          let L_a = hit.color * 0.1;

          let A = r.origin;
          let B = hit.position;

          let rho_s = 0.1;
          let rho_d = hit.color * 0.9;
          let s = 42.0;
          let pi = 3.14159;

          let w_0 = reflect(r.direction, hit.normal) / length(r.direction);
          let w_i = light.w_i;

          if (intersect_scene(&shadow_ray, &shadow_hit)) {
            return L_a;
          }

          let L_r =((rho_d / pi) + (rho_s * (s + 2) / (2 * pi)) * pow(max(dot(w_0, w_i), 0.0), s)) * light.L_i * max(dot(w_i, hit.normal), 0.0);

          return L_r;
        }

        fn glossy(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
          let L_r = phong(r, hit);
          return L_r + refractive(r, hit);
        }

        // Textures
        fn texture_nearest(texture: texture_2d<f32>, texcoords: vec2f, repeat: bool) -> vec3f {
          let res = textureDimensions(texture);
          let st = select(clamp(texcoords, vec2f(0), vec2f(1)), texcoords - floor(texcoords), repeat);
          let ab = st*vec2f(res);
          let UV = vec2u(ab + 0.5) % res;
          let texcolor = textureLoad(texture, UV, 0);
          return texcolor.rgb;
        }

        fn texture_linear(texture: texture_2d<f32>, texcoords: vec2f, repeat: bool) -> vec3f { 
          let res = textureDimensions(texture);
          let st = select(clamp(texcoords, vec2f(0), vec2f(1)), texcoords - floor(texcoords), repeat);
          let ab = st*vec2f(res);

          let UV = vec2u(ab);
          let UVP1 = (UV + 1) % res;
          let UVP1x = vec2u(UVP1.x, UV.y);
          let UVP1y = vec2u(UV.x, UVP1.y);

          let c = ab % 1;

          let color1 = textureLoad(texture, UV, 0);
          let color2 = textureLoad(texture, UVP1x, 0);
          let color3 = textureLoad(texture, UVP1y, 0); 
          let color4 = textureLoad(texture, UVP1, 0);          

          let interp1 = mix(color1, color2, c.x);
          let interp2 = mix(color3, color4, c.x);
          let interp3 = mix(interp1, interp2, c.y);

          return interp3.rgb;
        } 

        fn get_camera_ray(ipcoords: vec2f) -> Ray
        {
          const eye = vec3f(277.0, 275.0, -570.0);
          const lookAt = vec3f(277.0, 275.0, 0.0);
          const u = vec3f(0.0, 1.0, 0.0);

          let v = normalize(lookAt - eye);

          let b1 = normalize(cross(v, u));
          let b2 = cross(b1, v);

          var ray: Ray;
          ray.origin = eye;
          ray.direction = normalize(ipcoords.x*b1 + ipcoords.y*b2 + uniforms_f.cam_const * v);
          ray.tmin = 0.0;
          ray.tmax = 1.0e16;

          return ray;
        }

        fn shade(r: ptr<function, Ray>,hit: ptr<function, HitInfo>) -> vec3f {
          switch (*hit).shader {
          case 1 { return lambertian(r, hit); }
          case 2 { return phong(r, hit); }
          case 3 { return mirror(r, hit); }
          case 4 { return refractive(r, hit); }
          case 5 { return glossy(r, hit); }
          case default { return (*hit).color; }
        }
      }

      fn intersect_scene(r: ptr<function, Ray>, hit : ptr<function, HitInfo>) -> bool {
        // Mirror sphere
        let center1 = vec3f(420.0, 90.0, 370.0);
        let radius1 = 90.0;
        let refractive_index1 = 0.0;

        // Glass sphere
        let center2 = vec3f(130.0, 90.0, 250.0);
        let radius2 = 90.0;
        let refractive_index2 = 1.5;

        if intersect_min_max(r) {
          if intersect_sphere((*r), hit, center1, radius1) {
            hit.shader = u32(uniforms_f.shader);

            if (dot(r.direction, hit.normal) > 0) {
              hit.ior1_over_ior2 = refractive_index1;
              hit.normal = -hit.normal;
            } else {
              hit.ior1_over_ior2 = 1 / refractive_index1;
            }

            r.tmax = hit.dist;
          }

          if intersect_sphere((*r), hit, center2, radius2) {
            hit.shader = u32(uniforms_f.shader);

            if (dot(r.direction, hit.normal) > 0) {
              hit.ior1_over_ior2 = refractive_index2;
              hit.normal = -hit.normal;
            } else {
              hit.ior1_over_ior2 = 1 / refractive_index2;
            }

            r.tmax = hit.dist;
          }

          intersect_trimesh((r), hit);
        }
        
        return hit.has_hit;
      }

      @fragment
      fn main_fs(@location(0) coords: vec2f) -> @location(0) vec4f {
        const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
        const max_depth = 10;
        let uv = vec2f(coords.x*uniforms_f.aspect*0.5f, coords.y*0.5f);
        
        var result = vec3f(0.0);

        let use_repeat = uniforms_ui.use_repeat != 0; let use_linear = uniforms_ui.use_linear != 0;
        let color = select(texture_nearest(my_texture, uv, use_repeat), texture_linear(my_texture, uv, use_repeat), use_linear);

        for(var j = 0u; j < uniforms_ui.number_of_jitters; j++) {
          var r = get_camera_ray(uv + jitter[j]);
          var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 1.5, vec2f(0.0), vec3f(0.0));
          for(var i = 0; i < max_depth; i++) {
            if (intersect_scene(&r, &hit)) {
                result += shade(&r, &hit);
            } else {
                result += bgcolor.rgb; break;
            }

            if hit.has_hit {
                break;
            }
          }
        } 

        result /= f32(uniforms_ui.number_of_jitters);
        return vec4f(pow(result, vec3f(1.0/uniforms_f.gamma)), bgcolor.a);
      }
    </script>
  </head>
  <body>
    <canvas id="webgpu-canvas" width="512" height="512">
      Please use a browser that supports HTML5 canvas.
    </canvas>
    <select id="shaderMenu">
      <option value="0">Lambertian</option>
      <option value="1">Phong</option>
      <option value="2">Mirror</option>
      <option value="3">Refractive</option>
      <option value="4" selected>Glossy</option>
    </select>
    <select id="addressmode" size="2">
      <option value="0">clamp-to-edge</option>
      <option value="1" selected>repeat</option>
    </select>
    <select id="filtermode" size="2">
      <option value="0">nearest</option>
      <option value="1" selected>linear</option>
    </select>
    <button id="inc_subdiv">Increase subdivisions</button>
    <button id="dec_subdiv">Decrease subdivisions</button>
  </body>
</html>
